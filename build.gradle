plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

ext {
    excludedPackages = [
            '**/store/myproject/onlineshop/global/config',
            '**/store/myproject/onlineshop/global/aop',
            '**/store/myproject/onlineshop/global/db',
            '**/store/myproject/onlineshop/global/annotation',
            '**/store/myproject/onlineshop/global/init',
            '**/store/myproject/onlineshop/global/event',
            '**/store/myproject/onlineshop/exception/**',
            '**/store/myproject/onlineshop/domain/**',
            '**/OnlineshopApplication.class'
    ]
}

jacocoTestReport {
    dependsOn test
    reports {
        html.required = true
        xml.required = true
    }
    def Qdomains = []
    for (qPattern in '**/QA'..'**/QZ') {
        Qdomains.add(qPattern + '*')
    }
    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it,
                            excludes: [] + Qdomains + excludedPackages)
                })
        )
    }

    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {

    def Qdomains = []
    for (qPattern in '*.QA'..'*.QZ') {
        Qdomains.add(qPattern + '*')
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it,
                            excludes: [] + Qdomains + excludedPackages)
                })
        )
    }

    violationRules {
        rule {
            element = 'CLASS'
            enabled = true

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.6
            }

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.6
            }
        }
    }
}

group = 'store.myproject'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

allprojects {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.assertj:assertj-core"

    // H2 DB (테스트용)
    testImplementation 'com.h2database:h2'

    // Instancio
    testImplementation 'org.instancio:instancio-junit:5.3.0'

    // Faker
    testImplementation 'net.datafaker:datafaker:2.4.3'

    // JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    // MYSQL
    runtimeOnly 'com.mysql:mysql-connector-j'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.4'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis:4.0.1'

    // Redisson
    implementation 'org.redisson:redisson-spring-boot-starter:4.1.0'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'

    // AWS S3
    implementation platform('software.amazon.awssdk:bom:2.29.46')
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:sts'

    // AOP
    implementation 'org.springframework.boot:spring-boot-starter-aop:4.0.0-M2'

    // 메일 발송
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // 포트원 REST API
    implementation 'com.github.iamport:iamport-rest-client-java:0.2.23'

    // UUID v7
    implementation 'com.github.f4b6a3:uuid-creator:6.1.1'

    // Spring Batch
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    testImplementation 'org.springframework.batch:spring-batch-test'

    // MyBatis
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:4.0.1'

    // PageHelper
    implementation 'com.github.pagehelper:pagehelper-spring-boot-starter:2.1.1'

    // Jackson3
    implementation 'tools.jackson.core:jackson-databind:3.0.3'
    implementation 'tools.jackson.core:jackson-core:3.0.3'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:3.0-rc5'
    implementation 'tools.jackson.datatype:jackson-datatype-jsr310:3.0.0-rc2'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy 'jacocoTestReport'
}